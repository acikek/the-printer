plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'org.quiltmc.quilt-mappings-on-loom' version '4.2.0'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'java'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/theprinter.accesswidener")
}

repositories {
	mavenCentral()

	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}

	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}

	maven {
		url = "https://maven.bai.lol"
	}
}

configurations {
	shade
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	mappings "org.quiltmc:quilt-mappings:${project.minecraft_version}+build.${project.quilt_mappings}:intermediary-v2"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// EvalEx
	String evalEx = "com.udojava:EvalEx:${project.evalex_version}"
	implementation evalEx
	shade evalEx

	// Data Criteria
	modImplementation include("maven.modrinth:data-criteria:${project.datacriteria_version}")

	// EMI
	modImplementation "dev.emi:emi:${project.emi_version}"

	// WTHIT API
	modCompileOnly "mcp.mobius.waila:wthit-api:fabric-${project.wthit_version}"

	// WTHIT Runtime
	modRuntimeOnly "mcp.mobius.waila:wthit:fabric-${project.wthit_version}"
	modRuntimeOnly "lol.bai:badpackets:fabric-0.2.0"

	// Sodium Compatibility Testing
	if (project.test_sodium == "true") {
		modRuntimeOnly "maven.modrinth:sodium:mc${project.sodium_version}"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

shadowJar {
	configurations = [project.configurations.shade]
	classifier "dev-shadow"
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {

	}
}
